# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

pkgname='gst-plugins-gl-git'
pkgver=0.11.0.1.1083.13b183a
pkgrel=1
pkgdesc="GStreamer Multimedia Framework OpenGL Plugins"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer-git')
options=(!libtool !emptydirs)
url="http://gstreamer.freedesktop.org/"

conflicts=('gst-plugins-gl')
provides=('gst-plugins-gl='$pkgver) 

depends=('gstreamer-git' 'libgl')

source='git://anongit.freedesktop.org/gstreamer/gst-plugins-gl'
sha256sums=('SKIP')

_gitname='gst-plugins-gl'

pkgver() {
  cd $_gitname
  version=$(grep AC_INIT configure.ac | sed 's/AC_INIT(GStreamer OpenGL Plug-ins, //' | sed 's/,//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  
  echo $version.$revision.$hash
}

build() {
  cd $_gitname
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
  --disable-static --enable-experimental
  make
}

package() {
  cd $_gitname
  make DESTDIR="${pkgdir}" install
}

