# Maintainer: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Scimmia

pkgname=wayland-git
pkgver=1.1.0.10.gb693088
pkgrel=1
pkgdesc="A modern display server."
arch=(i686 x86_64)
url="http://wayland.freedesktop.org"
provides=("wayland=${pkgver}")
conflicts=("wayland")
license=('MIT')
depends=('libffi')
makedepends=('git' 'expat')
options=(!libtool)
source=(git://anongit.freedesktop.org/wayland/wayland)
sha1sums=('SKIP')

pkgver() {
  cd "wayland"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "wayland"
  ./autogen.sh --prefix=/usr --disable-documentation
  make
}

package() {
  cd "wayland"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
