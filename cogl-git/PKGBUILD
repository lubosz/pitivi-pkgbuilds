# Maintainer: Yosef Or Boczko <yosefor3@walla.com>
# Contributor: Lubosz Sarnecki <lubosz@gmail.com>

pkgname=cogl-git
pkgver=2.0.0.2962.3d9eaef
pkgrel=1
pkgdesc="An object oriented GL/GLES Abstraction/Utility Layer"
arch=('i686' 'x86_64')
url="http://www.clutter-project.org/"
license=('GPL2')
depends=('libdrm' 'libxext' 'libxdamage' 'libxcomposite' 'gdk-pixbuf2' 'pango' 'wayland-git')
makedepends=('mesa' 'gobject-introspection')
options=(!libtool !emptydirs)
conflicts=('cogl')
replaces=('cogl')
provides=('cogl='$pkgver)

source=("git://git.gnome.org/cogl#branch=cogl-1.16")
#source=("git://git.gnome.org/cogl#branch=master")
sha256sums=("SKIP")
gitname=cogl

pkgver() {
  cd $gitname
  
  s="m4_define(\[cogl_"
  
  maj=$(grep $s"major" configure.ac | sed 's/'$s'major_version\], \[//' | sed 's/\])//')
  min=$(grep $s"minor_version" configure.ac | sed 's/'$s'minor_version\], \[//' | sed 's/\])//')
  mic=$(grep $s"micro_version" configure.ac | sed 's/'$s'micro_version\], \[//' | sed 's/\])//')
  
  version=$maj.$min.$mic
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  
  echo $version.$revision.$hash
}

build() {
  cd $gitname
  
  #patch -p1 < ../../cogl-gst.patch
  
  ./autogen.sh --prefix=/usr --enable-cairo=yes \
							 --enable-gdk-pixbuf=yes \
							 --enable-cogl-pango=yes \
							 --enable-glx=yes \
							 --enable-xlib-egl-platform=yes \
							 --enable-wayland-egl-platform=yes \
							 --enable-wayland-egl-server=yes \
							 --enable-introspection=yes \
							 --enable-unit-tests=no \
							 --enable-cogl-gst=yes
  make
}

package() {
  cd $gitname
  make DESTDIR="$pkgdir" install
}
