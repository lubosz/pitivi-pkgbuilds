# Maintainer: Kerrick Staley <mail@kerrickstaley.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer-git
pkgver=20121221
pkgrel=1
pkgdesc='GStreamer Multimedia Framework (Git version)'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://gstreamer.freedesktop.org/'
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection')
provides=('gstreamer=99')
conflicts=('gstreamer')
options=('!libtool')

_gitroot='git://anongit.freedesktop.org/gstreamer/gstreamer'
_gitname='gstreamer'

build() {
    cd "${srcdir}"
    
    msg 'Connecting to GIT server...'
    if [[ -d "${_gitname}" ]]; then
        (cd "${_gitname}"; git pull origin) || return 1
    else
        git clone --depth=1 "${_gitroot}" "${_gitname}" || return 1
    fi
    msg 'GIT checkout done'
    
    msg 'Starting make...'
    cd "${_gitname}"
    ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
        --libexecdir=/usr/lib --enable-gtk-doc --disable-static
    make
}

check() {
    cd "${srcdir}/${_gitname}"
    make check
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="${pkgdir}" install
}
