# Maintainer: NeoRaider <neoraider@universe-factory.net>

_pkgname=libxcb
pkgname=${_pkgname}-git
pkgver=20120307
pkgrel=1
pkgdesc="X11 client-side library (git version)"
arch=(i686 x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto-git' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python' 'autoconf')
conflicts=('libx11<1.1.99.2' 'libxcb')
provides=('libxcb=1.7')
options=('!libtool')
license=('custom')
source=(libxcb-1.1-no-pthread-stubs.patch)
sha1sums=('ea5ff407389b38de43652e022e270e6a8ab03158')

_gitroot="git://git.freedesktop.org/git/xcb/libxcb"
_gitname="libxcb"

build() {
  cd "${srcdir}"

  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/build"
  cd "${srcdir}/build"

  patch -Np1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"

  ./autogen.sh --prefix=/usr --enable-xinput
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
